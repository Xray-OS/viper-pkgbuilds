#https://github.com/calamares/calamares/releases
#change prepare number too

pkgname=arch7z-calamares-wayland
_pkgname=calamares
# change number in prepare as well
pkgver=3.3.15
pkgrel=06
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://codeberg.org/calamares/calamares/releases"
license=('LGPL')
#'appstream-qt5' out as dependency and on iso
provides=('calamares')
depends=(
	'boost-libs'
	'ckbcomp'
	'cryptsetup'
	'doxygen'
	'efibootmgr'
	'gptfdisk'
	'gtk-update-icon-cache'
	'icu'
	'kconfig'
	'kcoreaddons'
	'kcrash'
	'ki18n'
	'kparts'
	'kpmcore'
	'kservice'
	'kwidgetsaddons'
	'libpwquality'
	'mkinitcpio-openswap'
	'polkit-qt6'
	'rsync'
	'qt6-declarative'
	'solid'
	'squashfs-tools'
	'yaml-cpp'
)
conflicts=(
	'ada-calamares'
	'ada-calamares-next'
)
makedepends=(
	'boost'
	'cmake'
	'extra-cmake-modules'
	'git'
	'ninja'
	'python-jsonschema'
	'python-pyaml'
	'python-unidecode'
	'qt6-tools'
)
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source=("calamares::https://codeberg.org/Ada-OS/calamares/archive/$pkgver-$pkgrel.tar.gz"
	"cal-arch7z.desktop"
	"cal-arch7z-debugging.desktop"
	"calamares_polkit")

sha256sums=('e89419e18ff5425b45cc369e64ee34fe3ef9ae473844886ab0b8501eaf2dbfc7'
            '369148aa90e16a280b98bd1ba4948148d0944b9a63c661a947bdb2403c64bc82'
            '2e682cba41ba41446b49f0582c7de57b266d3e4f08b82d6ce376322bd8636a92'
            'cec894efaf4184ed282814a00d6e675d743c39a0a7dea756724fecbb24ca7eb3')

prepare() {

	cp -rv ../modules/* ${srcdir}/$_pkgname/src/modules/

	sed -i -e 's/"Install configuration files" OFF/"Install configuration files" ON/' "$srcdir/${_pkgname}/CMakeLists.txt"
	sed -i -e "s/desired_size = 512 \* 1024 \* 1024  \# 512MiB/desired_size = 512 \* 1024 \* 1024 \* 4  \# 2048MiB/" "$srcdir/${_pkgname}/src/modules/fstab/main.py"

	cd ${_pkgname}
	sed -i -e "s|CALAMARES_VERSION 3.3.14|CALAMARES_VERSION $pkgver-$pkgrel|g" CMakeLists.txt
	#sed -i -e '/property real padding: 16;/d' "$srcdir/${_pkgname}-${pkgver}-${pkgrel}/src/qml/calamares-qt5/slideshow/Presentation.qml"
	#sed -i -e '/property real padding: 16;/d' "$srcdir/${_pkgname}-${pkgver}-${pkgrel}/src/qml/calamares-qt6/slideshow/Presentation.qml"
}

build() {
	cd $_pkgname

    cmake -S . -Bbuild \
        -GNinja \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DWITH_APPSTREAM=OFF \
        -DWITH_PYBIND11=OFF \
        -DWITH_QT6=ON \
        -DSKIP_MODULES="dracut \
            dracutlukscfg \
            dummycpp \
            dummyprocess \
            dummypython \
            dummypythonqt \
            initramfs \
            initramfscfg \
            interactiveterminal \
            keyboardq \
            license \
            localeq \
            oemid \
            packagechooserq \
            partitionq \
            services-openrc \
            services-systemd \
            summaryq \
            tracking \
            usersq \
            welcomeq"

    cmake --build build
}

package() {
	cd ${_pkgname}/build
	DESTDIR="${pkgdir}" cmake --build . --target install
	install -Dm644 "$srcdir/cal-arch7z.desktop" "$pkgdir/usr/share/applications/cal-ada.desktop"
	install -Dm644 "$srcdir/cal-arch7z-debugging.desktop" "$pkgdir/usr/share/applications/cal-ada-debugging.desktop"
	install -Dm755 "$srcdir/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	rm "$pkgdir/usr/share/applications/calamares.desktop"
}

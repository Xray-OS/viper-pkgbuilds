# Maintainer: Jingbei Li <i@jingbei.li>
# Maintainer: Carlos Aznarán <caznaranl@uni.pe>
# Contributor: kastik <kastik69420@gmail.com>
# Contributor: Ismaël Bouya <ismael.bouya@normalesup.org>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=anaconda
pkgver=2025.06.1
_pkgver=${pkgver%.*}-${pkgver##*.}
pkgrel=1
pkgdesc="Simplifies package management and deployment of Anaconda"
arch=(x86_64 aarch64)
url="https://${pkgname}.com"
license=('custom')
provides=('conda')
optdepends=(
  'libxau: for Anaconda Navigator support'
  'libxi: for Anaconda Navigator support'
  'libxss: for Anaconda Navigator support'
  'libxtst: for Anaconda Navigator support'
  'libxcursor: for Anaconda Navigator support'
  'libxcomposite: for Anaconda Navigator support'
  'libxdamage: for Anaconda Navigator support'
  'libxfixes: for Anaconda Navigator support'
  'libxrandr: for Anaconda Navigator support'
  'libxrender: for Anaconda Navigator support'
  'mesa: for Anaconda Navigator support'
  'alsa-lib: for Anaconda Navigator support'
  'libglvnd: for Anaconda Navigator support'
  'xdg-utils: for menu integration'
)

source=(
  "${pkgname}-navigator.desktop"
  "${pkgname}-launcher"
  "${pkgname}-navigator.png"
)
source_x86_64=("https://repo.${pkgname}.com/archive/Anaconda3-${_pkgver}-Linux-x86_64.sh")
source_aarch64=("https://repo.${pkgname}.com/archive/Anaconda3-${_pkgver}-Linux-aarch64.sh")
options=(!strip libtool staticlibs)

sha512sums=(
  'a5165f4b53d9480988ee3a464d903e0e1b1ffd811938760e15eaf621ae75595a5a800cb85659829e59fd406216994366f428850f387cfc1527516ab29ebf1932'  # .desktop
  'bbdb0a74047578c9ba5613610085acb1c9a762969570761bd3f52cbd56ef4b5f1fda63e1cdfec643152f928899c8076137dbd825ec9af2c9b07d1f2f164217d1'  # launcher
  '2bea527fcb6917f7ecc0152d18ab4b0bc689bcc3f0ef57ecfb537446859d720cdfe40dff06007f6aed4fbd0f1bd1dd69be792098fdd74d4d19fbd38da2caf26a'  # icon
)
sha512sums_x86_64=('dbdffcf50539386c7a853fe386b0b10a34d93c8d78d1b370585e192d885f7e2e49e1ee2315dc899156cb47b1070b175744c78a85465ebbc0fe43d0fafd8db307')
sha512sums_aarch64=('7909645554dcedab4820f7758d81795e090091a93b62ba0a76d32bac4b9ea4f3e8cf9d9325bc07c47d3d81ebae6c9cf2c5620067a38618564ba77391710a23d1')

install="${pkgname}.install"

package() {
  prefix="${pkgdir}/opt/${pkgname}"
  LD_PRELOAD="/usr/lib/libfakeroot/libfakeroot.so"

  # Install Anaconda silently to /opt/anaconda
  bash "${srcdir}/Anaconda3-${_pkgver}-Linux-${CARCH}.sh" -b -p "${prefix}" -f
  cd "${prefix}"

  # Ensure package contents are readable
  chmod -R a+r pkgs

  # Remove hardcoded $pkgdir paths
  sed -e "s|${pkgdir}||g" -i $(grep "${pkgdir}" . -rIl 2>/dev/null)

  # Install license
  install -Dm644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.txt"

  # Install desktop launcher
  install -Dm644 "${srcdir}/${pkgname}-navigator.desktop" "${pkgdir}/usr/share/applications/${pkgname}-navigator.desktop"

  # Install wrapper script for GUI launcher
  install -Dm755 "${srcdir}/${pkgname}-launcher" "${pkgdir}/opt/anaconda/bin/${pkgname}-launcher"

  # Install icon for launcher visibility
  install -Dm644 "${srcdir}/${pkgname}-navigator.png" "${pkgdir}/usr/share/icons/hicolor/128x128/apps/${pkgname}-navigator.png"
}
